/*
 * Copyright 2014-2019 Netflix, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


plugins {
    id 'com.netflix.nebula.plugin-plugin' version '19.0.2'
}

compileGroovy.groovyOptions.configurationScript = file('src/groovyCompile/groovycConfig.groovy')

description 'Gradle plugin collect and provide information about the environment'

contacts {
    'nebula-plugins-oss@netflix.com' {
        moniker 'Nebula Plugins Maintainers'
        github 'nebula-plugins'
    }
}

dependencies {
    implementation gradleApi()

    // TODO, make these optional
    implementation 'com.perforce:p4java:2015.2.1365273'
    implementation 'com.netflix.nebula:nebula-gradle-interop:latest.release'
    implementation 'com.netflix.nebula:gradle-contacts-plugin:latest.release'
    implementation 'org.eclipse.jgit:org.eclipse.jgit:5.13.0.202109080827-r'
    implementation 'org.tmatesoft.svnkit:svnkit:1.8.12'
    implementation 'net.java.dev.jna:jna-platform:5.7.0'
    testImplementation 'com.github.stefanbirkner:system-rules:1.19.0'
    testImplementation ('org.ajoberstar.grgit:grgit-core:4.1.1') {
        exclude group: 'org.codehaus.groovy', module: 'groovy'
    }
}

test.exclude '**/*Local*'

pluginBundle {
    tags = ['nebula', 'info']
}

gradlePlugin {
    plugins {
        info {
            id = 'com.netflix.nebula.info'
            displayName = 'Gradle Info plugin'
            description = project.description
            implementationClass= 'nebula.plugin.info.InfoPlugin'
        }
        infoBasic {
            id = 'com.netflix.nebula.info-basic'
            displayName = 'Gradle Info Basic plugin'
            description = project.description
            implementationClass= 'nebula.plugin.info.basic.BasicInfoPlugin'
        }
        infoDependencies {
            id = 'com.netflix.nebula.info-dependencies'
            displayName = 'Gradle Info Dependencies plugin'
            description = project.description
            implementationClass= 'nebula.plugin.info.dependencies.DependenciesInfoPlugin'
        }
        infoBroker {
            id = 'com.netflix.nebula.info-broker'
            displayName = 'Gradle Info Broker plugin'
            description = project.description
            implementationClass= 'nebula.plugin.info.InfoBrokerPlugin'
        }
        infoCI {
            id = 'com.netflix.nebula.info-ci'
            displayName = 'Gradle Info CI plugin'
            description = project.description
            implementationClass= 'nebula.plugin.info.ci.ContinuousIntegrationInfoPlugin'
        }
        infoJAR {
            id = 'com.netflix.nebula.info-jar'
            displayName = 'Gradle Info JAR plugin'
            description = project.description
            implementationClass= 'nebula.plugin.info.reporting.InfoJarManifestPlugin'
        }
        infoJava {
            id = 'com.netflix.nebula.info-java'
            displayName = 'Gradle Info Java plugin'
            description = project.description
            implementationClass= 'nebula.plugin.info.java.InfoJavaPlugin'
        }
        infoOwners {
            id = 'com.netflix.nebula.info-owners'
            displayName = 'Gradle Info Owners plugin'
            description = project.description
            implementationClass= 'nebula.plugin.info.basic.ManifestOwnersPlugin'
        }
        infoProps {
            id = 'com.netflix.nebula.info-props'
            displayName = 'Gradle Info Properties plugin'
            description = project.description
            implementationClass= 'nebula.plugin.info.reporting.InfoPropertiesFilePlugin'
        }
        infoScm {
            id = 'com.netflix.nebula.info-scm'
            displayName = 'Gradle Info SCM plugin'
            description = project.description
            implementationClass= 'nebula.plugin.info.scm.ScmInfoPlugin'
        }
    }
}

tasks.withType(Test) {
    if (JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_17)) {
        jvmArgs("--add-opens", "java.base/java.util=ALL-UNNAMED", "--add-opens", "java.base/java.lang=ALL-UNNAMED")
    }
}
